//
// Copyright (c) 2016 eppz! mobile, Gergely BorbÃ¡s (SP)
//
// http://www.twitter.com/_eppz
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
// INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
// PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
// HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
// CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
// OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//

apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    provided files('./libs/UnityPlayer.jar') // Avoid redundant Unity Player in final application APK
}

android.libraryVariants.all { variant ->
    task(
            "copy${variant.name.capitalize()}PluginArchive",
            dependsOn: "assemble${variant.name.capitalize()}",
            type: Copy)
            {
                String assetFolder = "Assets/Plugins/eppz! Alert/Android";
                String archiveName = "${project.name}-${variant.name}.aar"
                String aarFolder = "${buildDir.getPath()}/outputs/aar/"
                String pluginFolder = "${rootDir.getPath()}/../../${assetFolder}"

                logger.lifecycle("${variant.name.capitalize()} AAR Folder: ${aarFolder}")
                logger.lifecycle("${variant.name.capitalize()} Plugin Folder: ${pluginFolder}")
                logger.lifecycle("${variant.name.capitalize()} Archive name: ${archiveName}")

                from("${aarFolder}")
                into("${pluginFolder}")
                include("${archiveName}")
                rename("${archiveName}", "eppz! Alert.aar")
            }
}